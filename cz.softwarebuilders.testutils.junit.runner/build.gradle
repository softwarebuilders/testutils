apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'cz.softwarebuilders'
archivesBaseName = "cz.softwarebuilders.testutils"
version = '0.7'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


jar {
    manifest {
        attributes 'Implementation-Title': 'cz.softwarebuilders.testutils.runner', 'Implementation-Version': version
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'testutils-junit-runner'
                packaging 'jar'
                // optionally artifactId can be defined here
                description "junit library with runner which keeps order of execution of testing methods"
                url 'https://github.com/softwarebuilders/testutils'

                scm {
                    connection 'https://github.com/softwarebuilders/testutils.git'
                    developerConnection 'https://github.com/softwarebuilders/testutils.git'
                    url 'https://github.com/softwarebuilders/testutils'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'hattorihanso'
                        name 'Jan Klimes'
                        email 'hans.klimes@gmail.com'
                    }
                }
            }
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    compile group: 'junit', name: 'junit', version: '4.5'
}




